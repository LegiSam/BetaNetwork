
    model {
    
    #True interaction probability for each bird, plant
    for (i in 1:Birds){
      for(j in 1:Plants){
          logit(s[i,j])<-alpha[i,j]
        }
    }

    #Observation Model
    for (x in 1:Nobs){
      
      #Detection Process
      z[x] ~ dbern(detect[Bird[x]]) 
      
      #Observation
      p[x]<-z[x] * s[Bird[x],Plant[x]]
      Yobs[x] ~ dbern(p[x])
    }
    
    #Assess Model Fit - Predict remaining data

    for(x in 1:Nnewdata){
    
      #Generate prediction
      znew[x] ~ dbern(detect[NewBird[x]])
      pnew[x] <- znew[x]*s[NewBird[x],NewPlant[x]]
  
      #Predicted observation
      Ynew_pred[x]~dbern(pnew[x])
      
      #Assess fit, proportion of corrected predicted links
      Enew[x]<-abs(Ynew[x]-Ynew_pred[x])
    
    }
    
    #Priors
    #Observation model
    #Detect priors, logit transformed - Following lunn 2012 p85
    for(x in 1:Birds){
      logit(detect[x])<-dcam[x]
      dcam[x]~dnorm(omega_mu,omega_tau)
    }
    
    #Process Model
    #Species level priors
    for (i in 1:Birds){
      for (j in 1:Plants){
        #Logit Intercept
        alpha[i,j] ~ dnorm(0,0.386)
      } 
    }
    
    #OBSERVATION PRIOR
    omega_mu ~ dnorm(0,0.386)
    omega_tau ~ dunif(0,10)
    
    #derived posterior predictive error
    fitnew<-sum(Enew[])
    
    }
    
