
    model {
    
    #Ecological Process Model - True interaction state
    for (i in 1:Birds){
    for(j in 1:Plants){
  
      #Interaction
      logit(s[i,j])<-alpha[i,j]

      for(k in 1:Cameras){
    
        #Occurrence 
        logit(psi[i,j,k])<-alpha_occ[i] + beta_occ[i] * elevation[k] 
        occ[i,j,k] ~ dbern(psi[i,j,k])
    
        #Conditional probability of interaction|occurrence
        rho[i,j,k] <- s[i,j] * occ[i,j,k]
        p[i,j,k] ~ dbern(rho[i,j,k])
    }

    }
    }

    
    #Observation Model
    for (x in 1:Nobs){
    
    #Detection Process
    z[x] <- detect[Bird[x]] * p[Bird[x],Plant[x],Camera[x]]

    #Observation, conditional on detection and occurrence.
    Yobs[x] ~ dbern(z[x])

    }
    
    #Priors
    
    #Occurrence model
    for(x in 1:Birds){
    alpha_occ[x] ~ dnorm(0,0.386)
    beta_occ[x] ~ dnorm(0,0.386)
    }
    
    #Observation model
    #Detect priors, logit transformed - Following lunn 2012 p85
    for(x in 1:Birds){
    logit(detect[x])<-dcam[x]
    dcam[x]~dnorm(omega_mu,omega_tau)
    }
    
    #Process Model
    for (i in 1:Birds){
    for (j in 1:Plants){
    #Intercept
    #logit prior, then transform for plotting
    alpha[i,j] ~ dnorm(0,0.386)
    }
    }
    
    #Observation group prior
    omega_mu ~ dnorm(0,0.386)
    omega_tau ~ dunif(0,10)
    
    }
    
